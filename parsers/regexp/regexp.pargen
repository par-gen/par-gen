#######################################
#
# Tokens
#
CHARACTER := '[^\(\)\[\]\|\.\*?+\r\n\b\t\frnbtf\\-^x0-9]';
NUMBER := '[0-9]';
CONTROL_CHARACTER := '[rnbtf]';
BACKSLASH := '\\';
X := 'x';
CARET := '^';
DASH := '-';
DOT := '\.';
UNION := '\|';
PAREN_OPEN := '\(';
PAREN_CLOSE := '\)';
QUANTIFIER := '[\*?+]';
BRACKET_OPEN := '\[';
BRACKET_CLOSE := '\]';

#######################################
#
# Rules
#

RegExp := Expression;

Expression := Union | Sequence | Atom;

Union :=
  | Union UNION Sequence
  | Union UNION Atom
  | Union UNION
  | Sequence UNION Sequence
  | Sequence UNION Atom
  | Sequence UNION
  | Atom UNION Sequence
  | Atom UNION Atom
  | Atom UNION
  ;

Sequence :=
    Atom Atom
  | Sequence Atom
  ;

Atom :=
    Character
  | Character QUANTIFIER
  | Group
  | Group QUANTIFIER
  | CharacterClass
  | CharacterClass QUANTIFIER
  ;

Group :=
    PAREN_OPEN Union PAREN_CLOSE
  | PAREN_OPEN Sequence PAREN_CLOSE
  | PAREN_OPEN Atom PAREN_CLOSE
  ;

CharacterClass :=
    BRACKET_OPEN CharacterClassCharacters BRACKET_CLOSE
  | BRACKET_OPEN CARET CharacterClassCharacters BRACKET_CLOSE
  ;

CharacterClassCharacters :=
    CharacterClassCharacter
  | CharacterClassCharacters CharacterClassCharacter
  ;

CharacterClassCharacter :=
    CharacterOrNumberOrX
  | CONTROL_CHARACTER
  | BACKSLASH DASH
  | BACKSLASH CARET
  | QUANTIFIER
  | PAREN_OPEN
  | PAREN_CLOSE
  | UNION
  | DOT
  | BACKSLASH BACKSLASH
  | BACKSLASH CONTROL_CHARACTER
  | BACKSLASH BRACKET_OPEN
  | BACKSLASH BRACKET_CLOSE
  | CharacterClassRange
  ;

CharacterClassRange :=
    CharacterOrNumberOrX DASH CharacterOrNumberOrX
  | CharacterOrNumberOrX DASH CONTROL_CHARACTER
  | CONTROL_CHARACTER DASH CharacterOrNumberOrX
  | CONTROL_CHARACTER DASH CONTROL_CHARACTER
  ;

Character :=
    CharacterOrNumberOrX
  | CONTROL_CHARACTER
  | CARET
  | DASH
  | BACKSLASH EscapedCharacter
  | BACKSLASH X NUMBER NUMBER
  ;

CharacterOrNumberOrX :=
    CHARACTER
  | NUMBER
  | X
  ;

EscapedCharacter :=
    CONTROL_CHARACTER
  | BACKSLASH
  | DOT
  | UNION
  | PAREN_OPEN
  | PAREN_CLOSE
  | BRACKET_OPEN
  | BRACKET_CLOSE
  | QUANTIFIER
  ;
