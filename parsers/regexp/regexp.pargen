#######################################
#
# Tokens
#
CHARACTER := '[^()\[\]|.*?+\r\n\b\t\frnbtf\-\^x0-9a-f]';
HEX_NUMBER := '[0-9c-ea]';
CONTROL_CHARACTER := '[rnt]';
BACKSLASH := '\\';
X := 'x';
B := 'b';
F := 'f';
CARET := '^';
DASH := '-';
DOT := '\.';
UNION := '\|';
PAREN_OPEN := '\(';
PAREN_CLOSE := '\)';
QUANTIFIER := '[*?+]';
BRACKET_OPEN := '\[';
BRACKET_CLOSE := '\]';

#######################################
#
# Rules
#

RegExp := Expression;

Expression := Union | Sequence | Atom;

Union :=
  | Union UNION Sequence
  | Union UNION Atom
  | Union UNION
  | Sequence UNION Sequence
  | Sequence UNION Atom
  | Sequence UNION
  | Atom UNION Sequence
  | Atom UNION Atom
  | Atom UNION
  ;

Sequence :=
    Atom Atom
  | Sequence Atom
  ;

Atom :=
    Character
  | Character QUANTIFIER
  | Group
  | Group QUANTIFIER
  | CharacterClass
  | CharacterClass QUANTIFIER
  ;

Group :=
    PAREN_OPEN Union PAREN_CLOSE
  | PAREN_OPEN Sequence PAREN_CLOSE
  | PAREN_OPEN Atom PAREN_CLOSE
  ;

CharacterClass :=
    BRACKET_OPEN CharacterClassCharacters BRACKET_CLOSE
  | BRACKET_OPEN CARET CharacterClassCharacters BRACKET_CLOSE
  ;

CharacterClassCharacters :=
    CharacterClassCharacter
  | CharacterClassCharacters CharacterClassCharacter
  ;

CharacterClassCharacter :=
    CharacterOrNumberOrX
  | ControlCharacter
  | BACKSLASH DASH
  | BACKSLASH CARET
  | QUANTIFIER
  | PAREN_OPEN
  | PAREN_CLOSE
  | UNION
  | DOT
  | BACKSLASH BACKSLASH
  | BACKSLASH ControlCharacter
  | BACKSLASH BRACKET_OPEN
  | BACKSLASH BRACKET_CLOSE
  | CharacterClassRange
  ;

CharacterClassRange :=
    CharacterOrNumberOrX DASH CharacterOrNumberOrX
  | CharacterOrNumberOrX DASH ControlCharacter
  | ControlCharacter DASH CharacterOrNumberOrX
  | ControlCharacter DASH ControlCharacter
  ;

Character :=
    CharacterOrNumberOrX
  | ControlCharacter
  | CARET
  | DASH
  | BACKSLASH EscapedCharacter
  | BACKSLASH X HexNumber HexNumber
  ;

HexNumber :=
    HEX_NUMBER
  | B
  | F
  ;

CharacterOrNumberOrX :=
    CHARACTER
  | HexNumber
  | X
  ;

ControlCharacter :=
    CONTROL_CHARACTER
  | B
  | F
  ;

EscapedCharacter :=
    ControlCharacter
  | BACKSLASH
  | DOT
  | UNION
  | PAREN_OPEN
  | PAREN_CLOSE
  | BRACKET_OPEN
  | BRACKET_CLOSE
  | QUANTIFIER
  ;
